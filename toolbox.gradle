import io.lacasse.gradle.toolbox.tasks.BuildOperationDurationComparision

initscript {
    repositories {
        mavenLocal()
    }
    dependencies {
        classpath 'io.lacasse.gradle:gradle-analysis-toolbox:latest.integration'
    }
}

rootProject {
    tasks.withType(io.lacasse.gradle.toolbox.tasks.AbstractOperationsTraceTask) {
        group = 'Analysis Toolbox'
        sourceTrace = provider {
            def prop = findProperty('trace.source')
            if (prop == null) {
                throw new IllegalArgumentException('Please provide source operations trace via `-Ptrace.source=...`.')
            }
            return layout.projectDirectory.file(prop)
        }
        targetTrace = provider {
            def prop = findProperty('trace.target')
            if (prop == null) {
                throw new IllegalArgumentException('Please provide target operations trace via `-Ptrace.target=...`.')
            }
            return layout.projectDirectory.file(prop)
        }
    }

    tasks.withType(BuildOperationDurationComparision) {
        resultFile = layout.buildDirectory.file("${it.name}.csv")
    }

    task compareAllTasks(type: BuildOperationDurationComparision)

    task compareCppCompileTaskExecutionTime(type: BuildOperationDurationComparision) {
        filter = tasksWithType(CppCompile)
    }

    task compareCppHeaderProcessTime(type: BuildOperationDurationComparision) {
        filter = {
            it.detailsClassName == 'org.gradle.language.nativeplatform.internal.incremental.IncrementalCompileProcessor$1$ProcessSourceFilesDetails'
        }
    }
}
